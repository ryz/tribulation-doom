// Based on the SuperShotgun from Eriance's eriguns.wad

ACTOR T_SuperShotgun : Shotgun
{
	Game Doom
	Scale 0.8
	SpawnID 33
	Weapon.SelectionOrder 400
	Weapon.SlotNumber 3
	Weapon.AmmoUse 2
	Weapon.AmmoGive 8
	Weapon.AmmoType "Shell"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive2 8
	Weapon.AmmoType2 "Shell"
	AttackSound "weapons/erisshotf"
	Inventory.PickupMessage "$GOTSHOTGUN2"
	Obituary "$OB_MPSSHOTGUN"
	Decal "BulletChip"
	States
	{
	Ready:
		DSSG A 1 A_WeaponReady
		Wait
	Deselect:
		DSSG A 1 A_Lower
		Loop
	Select:
		DSSG A 0
		DSSG A 1 A_Raise
		Wait
	Fire: 
		DSSG A 3
		DSSG A 0 A_JumpIfInventory("Shell",2,1)
		Goto Ready
		DSSF A 0 A_CheckForReload(2, "FireDouble", 1)
		Goto ReloadAlt
	FireDouble: // Double barrel shot
		TNT1 A 0 Radius_Quake(3,3,0,1,0) // Screenshake
		DSSF A 0 A_FireBullets (11.2, 7.1, 20, 5, "BulletPuff")
		DSSF A 7 A_GunFlash 
		DSSF B 2 bright
		DSSF CJ 2
	        Goto Reload
	AltFire: // Check first, then fire left (second) barrel shto
		DSSF A 0 A_GunFlash
		DSSF A 0 A_PlaySound("weapons/sshot1bar", CHAN_AUTO)
		DSSF A 0 A_CheckForReload(2, "AltFire1") // Jump to first barrel shot
		TNT1 A 0 Radius_Quake(1,2,0,1,0) // Screenshake
		DSSF G 2 bright A_FireBullets(6.0, 3.0, 10, 5, "BulletPuff")
		DSSF H 2 bright
		DSSF IJ 2
		Goto Reload
	AltFire1: // Right (first) barrel shot
		TNT1 A 0 Radius_Quake(1,2,0,1,0) // Screenshake
		DSSF D 2 bright A_FireBullets(6.0, 3.0, 10, 5, "BulletPuff")
		DSSF E 2 bright
		DSSF FJ 2
		DSSG A 2
		Goto Ready
	Reload:
		DSSG B 2 A_CheckReload
		DSSG CD 2 A_ResetReloadCounter
		DSSG E 2 A_PlaySound("weapons/erisshoto", CHAN_WEAPON)
                DSSG A 0 A_FireCustomMissile("T_ShotSmoke_2_Spawner", 0, 0, -10, -1) // Left barrel
		DSSG A 0 A_FireCustomMissile("T_ShotSmoke_2_Spawner", 0, 0, -7, -1) // Right barrel
		TNT1 A 0 A_FireCustomMissile("T_ShellCasingSpawn", 5, 0, 0, -15)
		TNT1 A 0 A_FireCustomMissile("T_ShellCasingSpawn", 5, 0, 0, -15)
		DSSG FGHIJK 2
		DSSG L 2 A_PlaySound("weapons/erisshotl", CHAN_WEAPON)
		DSSG MNOP 2
	RL2:
		DSSG Q 2 A_PlaySound("weapons/erisshotl", CHAN_WEAPON)
		DSSG RS 2
		DSSG T 2 A_PlaySound("weapons/erisshotc", CHAN_WEAPON)
		DSSG UC 2
		Goto Ready
	ReloadAlt:
		DSSG CD 2 A_ResetReloadCounter
		DSSG E 2 A_PlaySoundEx("weapons/sshoto", "Weapon")
		DSSG A 0 A_FireCustomMissile("T_ShotSmoke_2_Spawner", 0, 0, -7, -1) // Right barrel
		TNT1 A 0 A_FireCustomMissile("T_ShellCasingSpawn", 5, 0, 0, -15)
		DSSG VWOP 2
		Goto RL2
	Flash:
		TNT1 A 1 Bright A_Light1
		TNT1 A 2 Bright A_Light2
		TNT1 A 2 Bright A_Light1
		Goto LightDone
	Spawn:
		DESS A -1
		Stop
	}
}
