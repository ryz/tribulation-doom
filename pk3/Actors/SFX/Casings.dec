// Casings code based on work from Alex_Mercer and Complex Doom
// Modified by ryz


// ===============
// Base Casings
// ===============

Actor T_CasingSpawnerBase {
    Speed 20
    Projectile
    +NoClip 
    +ClientsideOnly
}
 
Actor T_CasingBase {
    Speed 6
    +DoomBounce -NoGravity +WindThrust +ClientsideOnly
    +MoveWithSector +Missile +NoBlockmap -DropOff
    +NoTeleport +ForceXYBillboard +NotDMatch +Ghost
	
    // ThruActors is useful to prevent collision with monsters,
    // which would stop the velocity and sounds
    +ThruActors
}


// ===============
// Shotgun Casings
// ===============

Actor T_ShellCasingSpawn : T_CasingSpawnerBase {
    States {
        Spawn:
            TNT1 A 2 // These tics influence the "delay" before the casing is spawned
            TNT1 A 0 A_SpawnItemEx("T_ShotgunCasing",0,-8,0,Random(2,4),Random(3,6),Random(3,6),0)
            Stop
    }
}

Actor T_ShotgunCasing : T_CasingBase {
    Height 6
    Radius 3
    Scale 0.22
    BounceFactor 0.55
    WallBounceFactor 0.2
    BounceCount 4
    SeeSound "Weapons/ShellCasing1"
    States {
        Spawn:
            TNT1 A 0
            REDC ABCDEFGH 2 // A_SpawnItem ("T_ShotSmoke_2_Spawner") // Adds smoke trails to the casings, might be overkill
            Loop
        Death:
            TNT1 A 0
            TNT1 A 0 A_Jump(256, "Rest1", "Rest2") // rest on either side
        Rest1:
            REDC C 600
            Goto Fade
        Rest2:  
            REDC G 600
            Goto Fade
        Fade:
            "----" A 3 A_FadeOut (0.10)
            Loop
    }
}


// ===============
// Bullet Casings
// ===============

Actor T_BulletCasingSpawn : T_CasingSpawnerBase {
    States {
        Spawn:
            TNT1 A 2 // These tics influence the "delay" before the casing is spawned
            TNT1 A 0 A_SpawnItemEx("T_BulletCasing",0,-8,0,Random(2,4),Random(3,6),Random(3,6),0)
            Stop
    }
}

Actor T_BulletCasing : T_CasingBase {
    Height 6
    Radius 3
    Scale 0.18
    BounceFactor 0.5
    WallBounceFactor 0.25
    BounceCount 3
    SeeSound "Weapons/BulletCasing1"
    States {
        Spawn:
            TNT1 A 0
            CAS2 ABCDEFGH 2 // A_SpawnItem ("T_ShotSmoke_2_Spawner") // Adds smoke trails to the casings, might be overkill
            Loop
        Death:
            TNT1 A 0
            TNT1 A 0 A_Jump(256, "Rest1", "Rest2") // rest on either side
        Rest1:
            CAS2 C 600
            Goto Fade
        Rest2:  
            CAS2 G 600
            Goto Fade
        Fade:
            "----" A 3 A_FadeOut (0.10)
            Loop
    }
}
